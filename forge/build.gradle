plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "unclamped.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'ModMaven'
        url  = 'https://modmaven.dev/'
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation("com.simibubi.create:create-$minecraft_version:$create_version:slim") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:$registrate_version")
    modImplementation("com.jozufozu.flywheel:flywheel-forge-$minecraft_version:$flywheel_version")

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.5.0"))
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") == null ? '' : System.getenv("CURSEFORGE_TOKEN")
    project {
        id = '1368924'
        changelogType = 'markdown'
        changelog = rootProject.file("changelog.md")
        releaseType = 'release'
        addGameVersion '1.20.1'
        addGameVersion 'Forge'
        addGameVersion 'NeoForge'

        relations {
            requiredDependency 'create'
        }

        mainArtifact(jar) {
            displayName = "Create: Unclamped - Forge - $mod_version"
        }
    }
}

modrinth {
    loaders = ['forge','neoforge']
    versionNumber = "$mod_version"
    versionName = "Create: Unclamped - Forge - $mod_version"
    dependencies {
        required.project 'create'
    }
}
